plugins {
	id "java-library"

	id "checkstyle"
}

group = "org.hibernate.models"
version = "1.0.0-SNAPSHOT"

dependencies {
	implementation platform( libs.hibernatePlatform )
	implementation libs.hibernateCore
	implementation libs.hibernateModels
	implementation libs.jandex
	implementation libs.classmate
	implementation libs.logging

	implementation libs.hcann

	compileOnly libs.loggingAnnotations

	annotationProcessor libs.loggingProcessor
	annotationProcessor libs.logging
	annotationProcessor libs.loggingAnnotations

	testImplementation testLibs.junit5Api
	testImplementation testLibs.assertjCore
	testImplementation libs.hibernateTesting

	testRuntimeOnly testLibs.junit5Engine
	testRuntimeOnly testLibs.h2
}

java {
	sourceCompatibility = 17
	targetCompatibility = 17
}

test {
	useJUnitPlatform()
}

tasks.withType( JavaCompile ).configureEach {
	options.encoding = "UTF-8"
	options.warnings false
}

// create a single "compile" task
tasks.register( "compile" ) {compile->
	compile.dependsOn tasks.named( sourceSets.main.compileJavaTaskName )
	compile.dependsOn tasks.named( sourceSets.test.compileJavaTaskName )
}

checkstyle {
	sourceSets = [ project.sourceSets.main ]
	showViolations = false
}
// only include explicit sources (and thereby exclude generated sources)
tasks.checkstyleMain.source = 'src/main'